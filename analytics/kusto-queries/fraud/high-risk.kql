// Award API - High Risk Fraud Nominations
// Description: Shows all CRITICAL and HIGH risk fraud assessments from the last 7 days
// Last Updated: 2026-02-12
// Author: David Terian
// Category: Fraud Detection

ContainerAppConsoleLogs_CL
| where TimeGenerated > ago(7d)
| where ContainerAppName_s in ("award-api-eastus", "award-api-westus")
| extend LogData = parse_json(Log_s)
| where tostring(LogData.logger) == "fraud_ml"
| where tostring(LogData.message) == "Fraud assessment completed"
| where tostring(LogData.risk_level) in ("CRITICAL", "HIGH")
| extend 
    PacificTime = datetime_utc_to_local(TimeGenerated, 'US/Pacific'),
    RiskLevel = tostring(LogData.risk_level),
    FraudScore = toint(LogData.fraud_score),
    Warnings = tostring(LogData.warning_flags),
    NominatorId = toint(LogData.nominator_id),
    BeneficiaryId = toint(LogData.beneficiary_id)
| project PacificTime, RiskLevel, FraudScore, Warnings, NominatorId, BeneficiaryId, Region = ContainerAppName_s
| order by FraudScore desc, PacificTime desc
