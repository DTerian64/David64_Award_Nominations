// Award API - Email Success Rate
// Description: Calculates email success vs failure rate by region
// Last Updated: 2026-02-12
// Author: David Terian
// Category: Email Monitoring

ContainerAppConsoleLogs_CL
| where TimeGenerated > ago(24h)
| where ContainerAppName_s in ("award-api-eastus", "award-api-westus")
| extend LogData = parse_json(Log_s)
| where tostring(LogData.logger) == "email_utils"
| extend 
    Message = tostring(LogData.message),
    LogLevel = tostring(LogData.level)
| summarize 
    Total = count(),
    Success = countif(Message contains "successfully" or Message contains "sent successfully"),
    Failed = countif(LogLevel in ("ERROR", "WARNING"))
    by Region = ContainerAppName_s
| extend 
    SuccessRate = round(todouble(Success) / todouble(Total) * 100, 2),
    FailureRate = round(todouble(Failed) / todouble(Total) * 100, 2)
| project Region, Total, Success, Failed, SuccessRate, FailureRate
| order by Region asc
